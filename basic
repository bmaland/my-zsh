# (-*- shell-script -*-)

# Zsh options
setopt AUTO_CD
setopt CORRECT_ALL          # Try to correct the spelling of all arguments in a line
setopt EXTENDED_HISTORY     # Puts timestamps in the history
setopt HASH_LIST_ALL        # Make sure the entire command path is hashed first
setopt HIST_IGNORE_DUPS     # Ignores consecutive duplicate lines
setopt HIST_REDUCE_BLANKS   # Remove superfluous blanks from each command line being added to the history list
setopt INTERACTIVE_COMMENTS # Allow comments even in interactive shells
setopt NOTIFY               # Report the status of background jobs immediately
setopt GLOB_DOTS            # Do not require a leading `.' in a filename to be matched explicitly
setopt CDABLE_VARS          # Try to expand the expression as if it were preceded by a `~'
setopt AUTO_LIST            # Automatically list choices on an ambiguous completion
setopt AUTO_PUSHD           # Make cd push the old directory onto the directory stack
setopt PUSHD_SILENT         # Do not print the directory stack after pushd or popd
setopt REC_EXACT            # In completion, recognize exact matches even if they are ambiguous.
setopt IGNORE_EOF           # Do not exit on end-of-file
setopt MENU_COMPLETE        # On an ambiguous completion, insert the first match immediately
setopt FUNCTION_ARGZERO     # When  executing a shell function or sourcing a
                            # script, set $0 temporarily to the name of the function/script
setopt NO_BAD_PATTERN
setopt NO_BEEP

# These settings are not available in zsh pre 3.0.9
if [[ $ANCIENT_ZSH != t ]]; then
  setopt HIST_IGNORE_ALL_DUPS # Remove old dups
  setopt INC_APPEND_HISTORY   # New history lines are added to the $HISTFILE as soon as they are entered
  setopt NO_GLOBAL_RCS        # Ignore /etc/zshrc etc

# Autoload zsh modules when they are referenced
  zmodload -a zsh/stat stat
  zmodload -a zsh/zpty zpty
  zmodload -a zsh/zprof zprof

  autoload -U zmv
  autoload -U compinit
  compinit

  # Only alphanumeric characters are considered word characters
  autoload -U select-word-style
  select-word-style bash
fi

if [[ $MODERN_ZSH = t ]]; then
  autoload -U zsh-mime-setup
  zsh-mime-setup
fi

limit coredumpsize 0


# SHELL VARIABLES

# Use dircolors where available
if which dircolors > /dev/null; then
  eval $(dircolors -b)
else
  LS_COLORS='no=00:fi=00:di=01;34:ln=01;36:pi=33:so=01;35:bd=01;33:cd=01;33:
ex=01;32:*.c=36:*.cc=36:*.h=33:*.cmd=01;32:*.exe=01;32:*.com=01;32:
*.btm=01;32:*.bat=01;32:*.app=01;32:*.tar=00;31:*.tgz=00;31:*.arj=00;31:
*.taz=00;31:*.lzh=00;31:*.zip=00;31:*.z=00;31:*.Z=00;31:*.gz=00;31:
*.sit=00;31:*.sitX=00;31:*.zip=00;31:*.bin=00;31:*.hqx=00;31:*.jpg=00;35:
*.jpeg=00;35:*.gif=00;35:*.bmp=00;35:*.xbm=00;35:*.xpm=00;35:*.tif=00;35:
*.tiff=00;35:*.pdf=00;35:*.avi=00;35:*.mov=00;35:*.mpg=00;35:*.mpeg=00;35:
*.asf=00;35:*.wmv=00;35:*.rm=00;35:*.swf=00;35:*.mp3=00;35:*.aiff=00;35:
*.aif=00;35:*.snd=00;35:*.wav=00;35:';
  export LS_COLORS
fi
export ZLS_COLORS=$LS_COLORS

# Prompt
export PROMPT="$(print '%{\e[1;31m%}%m%{\e[0m%}'):%~ $ "

# History
export HISTSIZE=5000
export SAVEHIST=5000
export HISTFILE=~/.history

# Default path
export PATH="/opt/local/bin:/usr/local/bin:/usr/bin:/usr/sbin:/sbin:/bin"

export LIBXCB_ALLOW_SLOPPY_LOCK=true

export CLICOLOR=yes
export GREP_OPTIONS='--color=auto'
export GREP_COLOR='1;36'
export PAGER=less
export LESS="-iMSx4 -FX --quiet"
export PYTHONSTARTUP=${HOME}/.pystartup
export CLOJURE_HOME=${HOME}/src/clojure
export MAVEN_OPTS="-Xmx512m"

# -c, --create-frame      Create a new frame instead of trying to
#                         use the current Emacs frame
# -a, --alternate-editor=EDITOR
#                         Editor to fallback to if the server is not running
export EDITOR="emacsclient -n -c -a vim"
export VISUAL="emacsclient -n -c -a vim"

if [[ $ANCIENT_ZSH != t ]]; then
# Completion Styles

# type a directory's name to cd to it.
compctl -/ cd

# list of completers to use
zstyle ':completion:*::::' completer _complete _correct _approximate _ignored

# defaults
zstyle ':completion:*:default' list-prompt '%S%M matches%s'

# match uppercase from lowercase
zstyle ':completion:*' matcher-list 'm:{a-z}={A-Z}'

zstyle ':completion:*' expand 'yes'
zstyle ':completion:*:expand:*' accept-exact true

# Completion of hostnames, pulled out of ~/.ssh/config.
if [ -f ~/.ssh/config ]; then
  local _myhosts
  _myhosts=( $(cat ~/.ssh/config|grep '^H'|cut -d ' ' -f 2) )
  zstyle ':completion:*' hosts ${_myhosts[1,-2]} # Slices off the last entry
fi

# Disable completion of usernames
zstyle ':completion:*' users off

# Cache
zstyle ':completion:*' use-cache on
zstyle ':completion:*' cache-path ~/.zsh/cache

# x windows
zstyle ':completion:*:windows' menu on=0

# Expand man pages
zstyle ':completion:*:manuals' separate-sections true

# allow one error for every three characters typed in approximate completer
zstyle -e ':completion:*:approximate:*' max-errors \
    'reply=( $(( ($#PREFIX+$#SUFFIX)/2 )) numeric )'

# insert all expansions for expand completer
zstyle ':completion:*:expand:*' keep-prefix true tag-order all-expansions

# formatting and messages
zstyle ':completion:*' verbose yes
zstyle ':completion:*' list-separator '#'
zstyle ':completion:*:descriptions' format '%B%d%b'
zstyle ':completion:*:messages' format '%d'
zstyle ':completion:*:warnings' format 'No matches for: %d'
zstyle ':completion:*:corrections' format '%B%d (errors: %e)%b'
zstyle ':completion:*' group-name ''
zstyle ':completion:*' auto-description 'specify: %d'
zstyle ':completion:*:prefix:*' add-space true

# Ignore
zstyle ':completion:*:*files' ignored-patterns '*?.o' '*?~' '*\#'
zstyle ':completion:*:*directories' ignored-patterns '.*'
zstyle ':completion:*:*cd:*' ignored-patterns '(*/|)(CVS|SCCS|SVN|GIT|HG)'
zstyle ':completion::complete:*:functions' ignored-patterns '_*'
zstyle ':completion:*' ignore-parents parent pwd
# dont display same match twice for these
zstyle ':completion::*:(git|less|rm|emacs)' ignore-line true

# offer indexes before parameters in subscripts
zstyle ':completion:*:*:-subscript-:*' tag-order indexes parameters

# command for process lists, the local web server details and host completion
# on processes completion complete all user processes
zstyle ':completion:*:processes' command 'ps -au$USER'

## add colors to processes for kill completion
zstyle ':completion:*:*:kill:*:processes' list-colors '=(#b) #([0-9]#)*=0=01;31'

# SSH Completion
zstyle ':completion:*:scp:*' tag-order \
   files users 'hosts:-host hosts:-domain:domain hosts:-ipaddr"IP\ Address *'
zstyle ':completion:*:scp:*' group-order \
   files all-files users hosts-domain hosts-host hosts-ipaddr
zstyle ':completion:*:ssh:*' tag-order \
   users 'hosts:-host hosts:-domain:domain hosts:-ipaddr"IP\ Address *'
zstyle ':completion:*:ssh:*' group-order \
   hosts-domain hosts-host users hosts-ipaddr
zstyle '*' single-ignored show

# Colors in completion
zstyle ':completion:*:default' list-colors ${(s.:.)LS_COLORS}
fi
